#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive    : enable

#extension GL_EXT_mesh_shader             : require

#include "common.glsl"
#include "scene.glsl"

layout(binding = 0) uniform sampler2D depth;
layout(binding = 1, std140) uniform UboScene {
  SceneDesc scene;
  };

layout(local_size_x = 16, local_size_y = 16) in;
layout(triangles, max_vertices = 8, max_primitives = 12) out;

out gl_MeshPerVertexEXT {
  vec4 gl_Position;
  } gl_MeshVerticesEXT[];

const vec3 v[8] = {
  {0,0,0},{1,0,0},{1,1,0},{0,1,0},
  {0,0,1},{1,0,1},{1,1,1},{0,1,1},
  };
const int ibo[] = {
  0, 1, 3, 3, 1, 2,
  1, 5, 2, 2, 5, 6,
  5, 4, 6, 6, 4, 7,
  4, 0, 7, 7, 0, 3,
  3, 2, 7, 7, 2, 6,
  4, 5, 0, 0, 5, 1
  };

shared uint tileZint;

void main() {
  tileZint = 0;
  barrier();

  const ivec2 size = textureSize(depth,0);
  if(all(lessThan(gl_GlobalInvocationID.xy,size))) {
    float z = texelFetch(depth, ivec2(gl_GlobalInvocationID.xy), 0).r;
    atomicMax(tileZint, floatBitsToUint(z));
    }
  barrier();

  const float tileZ = uintBitsToFloat(tileZint);

  SetMeshOutputsEXT(8, 12);
  const uint id = gl_LocalInvocationIndex;
  if(gl_LocalInvocationIndex<8) {
    vec2 xy0 = vec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy)/vec2(size);
    vec2 xy1 = vec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy + gl_WorkGroupSize.xy)/vec2(size);

    vec3 pos = v[id];
    pos.xy = mix(xy0, xy1, pos.xy);
    pos.xy = pos.xy*2.0 - vec2(1);
    if(id>=4)
      pos.z = tileZ;

    const vec4 pos4 = scene.viewProjectInv * vec4(pos,1);
    gl_MeshVerticesEXT[id].gl_Position = scene.viewShadow[1] * pos4;
    }

  if(gl_LocalInvocationIndex<12) {
    uint ix = id*3;
    gl_PrimitiveTriangleIndicesEXT[id] = uvec3(ibo[ix+0], ibo[ix+1], ibo[ix+2]);
    }
  }
