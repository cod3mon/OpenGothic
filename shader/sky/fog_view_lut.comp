#version 450

#extension GL_EXT_control_flow_attributes : enable
#extension GL_GOOGLE_include_directive    : enable

#include "sky_common.glsl"
#include "scene.glsl"

layout(binding = 0) uniform sampler2D tLUT;
layout(binding = 1) uniform sampler2D mLUT;
layout(binding = 2) uniform sampler2D cloudsLUT;
#ifdef COMPUTE
layout(binding = 3) uniform sampler2D textureSm1;
layout(binding = 4) uniform writeonly image3D fogLut;
layout(binding = 5, std140) uniform UboScene {
  SceneDesc scene;
  };
#endif

layout(local_size_x = 8, local_size_y = 8) in;

void store(ivec3 at, vec3 lum, vec3 trans) {
  const float t = (trans.x+trans.y+trans.z)/3.0;
  imageStore(fogLut, at, vec4(lum,t));
  }

vec4 shadowSample(in sampler2D shadowMap, vec2 shPos) {
  shPos.xy = shPos.xy*vec2(0.5,0.5)+vec2(0.5);
  return textureGather(shadowMap,shPos);
  }

float shadowResolve(in vec4 sh, float z) {
  z  = clamp(z,0,0.99);
  sh = step(sh,vec4(z));
  return 0.25*(sh.x+sh.y+sh.z+sh.w);
  }

float calcShadow(vec3 shPos1) {
  //shPos1.y = max(0,shPos1.y);
  vec4  lay1 = shadowSample(textureSm1,shPos1.xy);
  float v1   = shadowResolve(lay1,shPos1.z);
  return v1;
  }

float shadowFactor(vec4 shPos) {
  if(abs(shPos.x)>=shPos.w && abs(shPos.y)>=shPos.w)
    return 1;
  return calcShadow(shPos.xyz/shPos.w);
  }

float shadowFactorMS(vec4 shPos0, vec4 shPos1, int maxStep) {
  float ret  = 0;
  for(int i=0; i<maxStep; ++i) {
    vec4 shPos = mix(shPos0,shPos1,float(i+0.5)/float(maxStep));
    ret += calcShadow(shPos.xyz/shPos.w);
    }
  return ret/float(maxStep);
  }

vec3 project(mat4 m, vec3 pos) {
  vec4 p = m*vec4(pos,1);
  return p.xyz/p.w;
  }

float tStep(int i, int steps) {
  float t = float(i+0.3)/steps;
  return t;
  }

void main() {
  const ivec3 dstSz = imageSize(fogLut);
  const ivec3 dstUV = ivec3(gl_GlobalInvocationID.xy,0);
  if(dstUV.x>=dstSz.x || dstUV.y>=dstSz.y)
    return;
  const vec2  inPos  = ((vec2(dstUV.xy)+vec2(0.5))/vec2(dstSz.xy))*2.0-vec2(1.0);
  const int   steps  = dstSz.z;

  const float dMin   = 0;
  const float dMax   = 0.9999;
  const vec3  pos0   = project(scene.viewProjectInv, vec3(inPos,dMin));
  const vec3  pos1   = project(scene.viewProjectInv, vec3(inPos,dMax));
  const vec4  shPos0 = scene.viewShadow[1]*vec4(pos0, 1);
  const vec4  shPos1 = scene.viewShadow[1]*vec4(pos1, 1);

  const float viewDistanceScale = 20;
  const vec3  ray               = pos1.xyz - pos0.xyz;
  const float dist              = length(ray)*0.01; // meters
  const vec3  dir               = normalize(ray);

  const vec3  sunDir             = vec3(push.sunDir);
  const float cosTheta           = dot(dir, sunDir);
  const float miePhaseValue      = miePhase(cosTheta);
  const float rayleighPhaseValue = rayleighPhase(-cosTheta);

  vec3  pos               = pos0*0.01 + vec3(0,RPlanet,0);
  vec3  sunTransmittance0 = textureLUT(tLUT, pos, sunDir);
  vec3  psiMS0            = textureLUT(mLUT, pos, sunDir);

  vec3  scatteredLight = vec3(0.0);
  vec3  transmittance  = vec3(1.0);
  // float oclusion       = 1.0 - textureLod(cloudsLUT, vec2(push.night,0), 0).a;
  float clouds         = textureLod(cloudsLUT, vec2(push.night,0), 0).a;

  for(int i=0; i<steps; ++i) {
    float t      = tStep(i,  steps);
    float tPrev  = tStep(i-1,steps);
    float dd     = (t - tPrev)*dist;

    vec3  pos    = mix(pos0,pos1,t)*0.01 + vec3(0,RPlanet,0);

    vec3  rayleighScattering;
    vec3  extinction;
    float mieScattering;
    scatteringValues(pos, clouds, rayleighScattering, mieScattering, extinction);

    vec3  sampleTransmittance = exp(-dd*extinction*viewDistanceScale);

#if defined(VOLUMETRIC_HQ)
    vec3  sunTransmittance    = sunTransmittance0;
    vec3  psiMS               = psiMS0;
#elif defined(VOLUMETRIC_LQ)
    int   samplecount         = max(32-i/2,1);
    float shadow              = shadowFactorMS(mix(shPos0,shPos1,tPrev),
                                               mix(shPos0,shPos1,t),
                                               samplecount);
    vec3  sunTransmittance    = sunTransmittance0 * shadow;
    vec3  psiMS               = psiMS0 * (shadow*0.9 + 0.1);
#else
    vec3  sunTransmittance    = textureLUT(tLUT, pos, sunDir);
    vec3  psiMS               = textureLUT(mLUT, pos, sunDir);
#endif

    vec3 rayleighInScattering = rayleighScattering*(rayleighPhaseValue*sunTransmittance + psiMS);
    vec3 mieInScattering      = mieScattering     *(miePhaseValue     *sunTransmittance + psiMS);
    vec3 inScattering         = (rayleighInScattering + mieInScattering);

    // Integrated scattering within path segment.
    // See slide 28 at http://www.frostbite.com/2015/08/physically-based-unified-volumetric-rendering-in-frostbite/
    vec3  scatteringIntegral  = (inScattering - inScattering * sampleTransmittance) / extinction;

    transmittance  *= sampleTransmittance;
    scatteredLight += scatteringIntegral*transmittance;

    store(ivec3(dstUV.xy,i), scatteredLight, transmittance);
    }
  }
